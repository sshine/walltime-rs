# Cross-compilation for the release artifacts.
#
# The tarballs are statically linked against musl so one binary per architecture
# runs on any Linux distribution, including Alpine, where a glibc-linked binary
# dies with a loader error that says nothing useful.
#
# Building through cargo with rust-overlay's target support, rather than through
# pkgsCross.*.rustPlatform, keeps this to seconds: every dependency is pure Rust, so
# the only thing missing for a foreign target is a linker.
{ ... }:
{
  perSystem =
    { pkgs, lib, ... }:
    let
      # rustc finds musl's crt objects in its own `self-contained` directory and can
      # link x86_64 unaided; only a foreign architecture needs a cross linker.
      aarch64-cc = pkgs.pkgsCross.aarch64-multiplatform-musl.stdenv.cc;
      aarch64-linker = "${aarch64-cc}/bin/aarch64-unknown-linux-musl-cc";
    in
    {
      devshells.default = {
        packages = [ aarch64-cc ];

        env = [
          {
            name = "CARGO_TARGET_AARCH64_UNKNOWN_LINUX_MUSL_LINKER";
            value = aarch64-linker;
          }
        ];
      };
    };
}
